import static org.jetbrains.kotlin.gradle.plugin.AbstractKotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath libs.kotlin.gradlePlugin
	}
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
	linuxArm64 {
		binaries.executable {
			entryPoint = 'main'

			// Work around https://youtrack.jetbrains.com/issue/KT-48082.
			linkerOpts(
				"-L${file("usr/lib").absolutePath}",
				"-ldrm",
				"-lmali",
				// Do not check for symbols within our shared libraries.
				"--allow-shlib-undefined",
			)
		}
		compilations.main.cinterops {
			create("lightswitch") {
				packageName("lightswitch")
				includeDirs(
					"usr/include",
					"usr/include/libdrm",
				)
				header("usr/include/GLES2/gl2.h")
				header("usr/include/GLES2/gl2ext.h")
				header("usr/include/EGL/egl.h")
				header("usr/include/EGL/eglext.h")
				header("usr/include/gbm.h")
				header("usr/include/xf86drm.h")
				header("usr/include/xf86drmMode.h")
			}
		}
	}

	sourceSets {
		commonMain {
			dependencies {
				implementation libs.jetbrains.compose.runtime
			}
		}
	}

	sourceSets.configureEach {
		languageSettings.optIn("kotlin.ExperimentalStdlibApi")
		languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
		languageSettings.optIn("kotlinx.cinterop.ExperimentalForeignApi")
	}
}

dependencies {
	add(PLUGIN_CLASSPATH_CONFIGURATION_NAME, libs.jetbrains.compose.compiler)
}
