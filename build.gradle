buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.kotlin.composePlugin
		classpath libs.kotlin.serializationPlugin
		classpath libs.spotless.gradlePlugin
	}
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

kotlin {
	linuxArm64 {
		binaries.executable {
			entryPoint = 'main'

			// Work around https://youtrack.jetbrains.com/issue/KT-48082.
			linkerOpts(
				"-L${file("device/lib").absolutePath}",
				"-ldrm",
				"-lmali",
				"-lfontconfig",
				// Do not check for symbols within our shared libraries.
				"--allow-shlib-undefined",
			)
		}
		compilations.main.cinterops {
			create("lightswitch") {
				packageName("lightswitch")
				includeDirs(
					"device/include",
					"device/include/libdrm",
				)
				header("device/include/GLES2/gl2.h")
				header("device/include/GLES2/gl2ext.h")
				header("device/include/EGL/egl.h")
				header("device/include/EGL/eglext.h")
				header("device/include/gbm.h")
				header("device/include/input.h")
				header("device/include/xf86drm.h")
				header("device/include/xf86drmMode.h")
			}
		}
	}

	sourceSets {
		commonMain {
			dependencies {
				implementation libs.jetbrains.compose.runtime
				implementation libs.jetbrains.compose.material3
				implementation libs.skiko
				implementation libs.ktor.network
				implementation libs.kotlinx.serialization.json
			}
		}
		commonTest {
			dependencies {
				implementation libs.assertk
			}
		}
	}

	sourceSets.configureEach {
		languageSettings.optIn("androidx.compose.foundation.ExperimentalFoundationApi")
		languageSettings.optIn("androidx.compose.runtime.InternalComposeApi")
		languageSettings.optIn("androidx.compose.ui.InternalComposeUiApi")
		languageSettings.optIn("androidx.compose.ui.text.ExperimentalTextApi")
		languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
		languageSettings.optIn("kotlinx.cinterop.ExperimentalForeignApi")
	}
}

apply plugin: 'com.diffplug.spotless'

spotless {
	kotlin {
		target("src/**/*.kt")
		ktlint(libs.ktlint.core.get().version)
			.editorConfigOverride([
				'ktlint_standard_filename': 'disabled',
				'ktlint_function_naming_ignore_when_annotated_with': 'Composable',
			])
			.customRuleSets([
				libs.ktlint.composeRules.get().toString(),
			])
	}
}
