buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath libs.kotlin.gradlePlugin
	}
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
	linuxArm64 {
		binaries.executable {
			entryPoint = 'main'

			// Work around https://youtrack.jetbrains.com/issue/KT-48082.
			linkerOpts(
				"-L${file("usr/lib").absolutePath}",
				"-ldrm",
				"-lmali",
				// Do not check for symbols within our shared libraries.
				"--allow-shlib-undefined",
			)
		}
		compilations.main.cinterops {
			create("lightswitch") {
				packageName("lightswitch")
				includeDirs(
					"usr/include",
					"usr/include/libdrm",
				)
				header("usr/include/gbm.h")
				header("usr/include/xf86drmMode.h")
			}
		}
	}

	sourceSets.configureEach {
		languageSettings.optIn("kotlin.ExperimentalStdlibApi")
		languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
		languageSettings.optIn("kotlinx.cinterop.ExperimentalForeignApi")
	}
}
